/datum/scripture/cogscarab
	name = "Summon Cogscarab"
	desc = "Summon a Cogscarab shell, which will be possessed by fallen Ratvarian soldiers. Takes longer the more cogscarabs are alive. Requires 30 vitality."
	tip = "Use Cogscarabs to fortify Reebe while the human servants convert and sabotage the crew."
	button_icon_state = "Cogscarab"
	power_cost = STANDARD_CELL_CHARGE * 0.5
	vitality_cost = 30
	invocation_time = 12 SECONDS
	invocation_text = list("My fallen brothers,", "Now is the time we rise", "to protect our Lord", "and achieve greatness!")
	category = SPELLTYPE_PRESERVATION
	cogs_required = 5
	invokers_required = 2
	fast_invoke_mult = 1

/datum/scripture/cogscarab/begin_invoke(mob/living/invoking_mob, obj/item/clockwork/clockwork_slab/slab, bypass_unlock_checks)
	invocation_time = 12 SECONDS + (6 SECONDS * SSthe_ark.cogscarabs.len)
	. = ..()

/datum/scripture/cogscarab/check_special_requirements(mob/user)
	. = ..()
	if(!.)
		return FALSE

	if(!on_reebe(invoker))
		to_chat(invoker, span_warning("You must do this on Reebe!"))
		return FALSE

	if(length(SSthe_ark.cogscarabs) > MAXIMUM_COGSCARABS)
		to_chat(invoker, span_warning("You can't summon anymore cogscarabs."))
		return FALSE

	if(GLOB.clock_ark?.current_state >= ARK_STATE_ACTIVE)
		to_chat(invoker, span_warning("It is too late to summon cogscarabs now, Ratvar is coming!"))
		return FALSE
	return TRUE

/datum/scripture/cogscarab/invoke_success()
	new /obj/effect/mob_spawn/ghost_role/drone/cogscarab(get_turf(invoker))
