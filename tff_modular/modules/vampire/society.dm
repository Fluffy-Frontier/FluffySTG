/datum/antagonist/vampire/proc/check_start_society()
	if(SSvsociety.can_fire || !CONFIG_GET(flag/allow_vampire_prince))
		return

	if(length(GLOB.all_vampires) >= 3)
		SSvsociety.start_time = world.time
		SSvsociety.can_fire = TRUE
		message_admins("Vampire Society has started, as there are [length(GLOB.all_vampires)] vampires active.")
		log_game("Vampire Society has started, as there are [length(GLOB.all_vampires)] vampires active.")

/datum/antagonist/vampire/proc/princify()
	rank_up(8, TRUE) // Rank up a lot.
	to_chat(owner.current, span_cult_bold("As a true prince, you find some of your old power returning to you!"))
	owner.current.playsound_local(null, 'tff_modular/modules/vampire/sound/prince.ogg', 100, FALSE, pressure_affected = FALSE)
	prince = TRUE
	add_team_hud(owner.current)

	var/full_name = return_full_name()
	for(var/datum/antagonist/vampire/vampire as anything in GLOB.all_vampires)
		to_chat(vampire.owner.current, span_narsiesmall("[full_name], also known as [owner.name || owner.current.real_name || owner.current.name], has claimed the role of Prince!"))
	if(CONFIG_GET(flag/allow_vampire_scourge))
		grant_power(new /datum/action/cooldown/vampire/targeted/scourgify)

	var/datum/objective/vampire/prince/prince_objective = new()
	objectives += prince_objective
	owner.announce_objectives()

	message_admins("[ADMIN_LOOKUP(owner.current)] has received the role of Vampire Prince.")
	log_game("[key_name(owner.current)] has become the Vampire Prince.")

	notify_ghosts(
		"[owner.name] has become the Vampire Prince!",
		source = owner.current,
		notify_flags = NOTIFY_CATEGORY_NOFLASH,
		header = "bloodclan confirmed???",
	)

	update_static_data_for_all_viewers()
	tgui_alert(owner.current, "Congratulations, you have been chosen for Princedom.\nPlease note that this entails a certain responsibility. Your job, now, is to keep order, and to enforce the masquerade.", "Welcome, my Prince.", list("I understand"), 30 SECONDS, TRUE)

/**
 * Turns the player into a scourge.
**/
/datum/antagonist/vampire/proc/scourgify()
	ASSERT(!prince, "Somehow a prince was going to be turned into a scourge") // Literally how would this happen. Still, just in case.

	rank_up(4, TRUE) // Rank up less.
	to_chat(owner.current, span_cult_bold("As a Camarilla scourge, your newfound purpose empowers you!"))
	owner.current.playsound_local(null, 'tff_modular/modules/vampire/sound/scourge_recruit.ogg', 100, FALSE, pressure_affected = FALSE)
	scourge = TRUE
	add_team_hud(owner.current)

	var/datum/objective/vampire/scourge/scourge_objective = new()
	objectives += scourge_objective
	owner.announce_objectives()

	for(var/datum/antagonist/vampire as anything in GLOB.all_vampires)
		to_chat(vampire.owner.current, span_cult_bold(span_big("Under authority of the Prince, [owner.name || owner.current.real_name || owner.current.name] has been raised to the duty of the Scourge!")))

	message_admins("[ADMIN_LOOKUPFLW(owner.current)] has been made a Scourge of the Vampires!")
	log_game("[key_name(owner.current)] has become a Scourge of the Vampires.")

	notify_ghosts(
		"[owner.name] has been raised to the duty Scourge of the Vampires!",
		source = owner.current,
		notify_flags = NOTIFY_CATEGORY_NOFLASH,
		header = "bloodclan confirmed???",
	)

	update_static_data_for_all_viewers()

// We could put this in objectives but like, it's just two tiny hardcoded things. It's fine here.
/datum/objective/vampire/scourge
	name = "Camarilla Scourge"
	explanation_text = "Obey your prince! Ensure order! Safeguard the Masquerade!"
	completed = TRUE

/datum/objective/vampire/prince
	name = "Camarilla Prince"
	explanation_text = "Rule your fellow kindred with an iron fist! Ensure the sanctity of the Masquerade, at ALL costs!"
	completed = TRUE
